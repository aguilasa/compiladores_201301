#Options
GenerateScanner = true
GenerateParser = true
Language = Java
ScannerName = Lexico
ParserName = Sintatico
SemanticName = Semantico
Package = br.com.furb.comp.gals
ScannerCaseSensitive = true
ScannerTable = Compact
Input = String
Parser = LL
#RegularDefinitions
mai: [A-Z]
min: [a-z]
dig: [0-9]
letras: [a-zA-Z]
com_bloco: \{[^\}]*\}
com_linha: \\\\[^\n]*

#Tokens
palavra_reservada: {min}+ ({mai} {min}+)*

id_int: i_{letras}({letras} | {dig} | _ )*

id_float: f_{letras}({letras} | {dig} | _ )*

id_string: s_{letras}({letras} | {dig} | _ )*

id_bool: b_{letras}({letras} | {dig} | _ )*

const_int: {dig}+

const_float: {dig}+,{dig}+

const_string: \"[^\n\"]*\"

//ignorar comentários e espaços em branco
:!{com_bloco}
:!{com_linha}
: [\ \t\n\r]


//palavras reservadas
and = palavra_reservada : "and"
false = palavra_reservada : "false"
if = palavra_reservada : "if"
in = palavra_reservada : "in"
isFalseDo = palavra_reservada : "isFalseDo"
isTrueDo = palavra_reservada : "isTrueDo"
main = palavra_reservada : "main"
module = palavra_reservada : "module"
not = palavra_reservada : "not"
or = palavra_reservada : "or"
out = palavra_reservada : "out"
return = palavra_reservada : "return"
true = palavra_reservada : "true"
while = palavra_reservada : "while"

//símbolos especiais
","
":"
";"
"["
"]"
"("
")"
"+"
"-"
"*"
"/"
"<-"
"="
"!="
"<"
"<="
">"
">="

#NonTerminals
<main_module> 
<lista_de_modulos> 
<lista_comandos>
<lista_comandos1>
<modulo>
<lista_cmd_mod> 
<identificador> 
<parametros> 
<lista_de_identificadores> 
<lista_de_identificadores1>
<declaracao_variavel> 
<lista_de_variaveis> 
<atribuicao> 
<entrada> 
<saida> 
<lista_de_expressoes> 
<lista_de_expressoes1>
<true_do> 
<false_do> 
<false_do_op> 
<if> 
<true_or_false_do> 
<while> 
<comando> 
<expressao>
<expressao1>
<valor>
<relacional>
<aritmetica>
<aritmetica1>
<operador_relacional>
<termo>
<termo1>
<fator>
<fator1>
<relacional1>
#Grammar
<main_module> ::= #1 main module "[" <lista_de_modulos> #2 <lista_de_variaveis> <lista_comandos> "]" #3;
<lista_de_modulos> ::= î | <modulo><lista_de_modulos> ;

<lista_comandos> ::= <comando><lista_comandos1>;
<lista_comandos1> ::= î |<lista_comandos> ;

<modulo> ::= module <identificador> #4 <parametros> #5 "[" <lista_de_variaveis> <lista_cmd_mod> return <expressao> ";" "]" #6 ;
<lista_cmd_mod> ::= î | <comando> <lista_cmd_mod> ;
<identificador> ::= id_int | id_float | id_string | id_bool ;
<parametros> ::= ":" <lista_de_identificadores> #7 | î ;

<lista_de_identificadores> ::= <identificador> #9 <lista_de_identificadores1> ;

<lista_de_identificadores1> ::= î | "," <lista_de_identificadores>;

<declaracao_variavel> ::= ":" <lista_de_identificadores> #8 ";" ;

<lista_de_variaveis> ::= î | <declaracao_variavel> <lista_de_variaveis> ;

<atribuicao> ::= <identificador> #9 "<-" <expressao> #10 ";" ;

<entrada> ::= in "(" <lista_de_identificadores> ")" #11 ";" ;

<saida> ::= out "(" #12 <lista_de_expressoes> ")" ";" ;

<lista_de_expressoes> ::= <expressao> #13 <lista_de_expressoes1>;
<lista_de_expressoes1> ::= î  | "," <lista_de_expressoes> ;

<true_do> ::= isTrueDo ":" "[" <lista_comandos> "]" ;

<false_do> ::= isFalseDo ":" "[" <lista_comandos> "]" ;

<false_do_op> ::= î | #16 <false_do> ;

<if> ::= if "(" <expressao> #14 ")" <true_do> <false_do_op> #15 ;

<true_or_false_do> ::= isTrueDo #18 ":" "[" <lista_comandos> "]" | isFalseDo #18 ":" "[" <lista_comandos> "]" ;

<while> ::= while #17 "(" <expressao> ")" <true_or_false_do> #19 ;

<comando> ::= <atribuicao> | <entrada> | <saida> | <if> | <while> ;

<expressao> ::= <valor><expressao1>;
<expressao1> ::= î | or <valor> #20 <expressao1> | and <valor> #21 <expressao1>;

<valor> ::= <relacional> | true #22 | false #23 | not <valor> #24 ;

<relacional> ::= <aritmetica> <relacional1>; 
<relacional1> ::= î | <operador_relacional> #25 <aritmetica> #26;
<operador_relacional> ::= "=" | "!=" | "<" | "<=" | ">" | ">=" ;

<aritmetica> ::= <termo><aritmetica1>;
<aritmetica1> ::= î | "+" <termo> #27 <aritmetica1> | "-" <termo> #28 <aritmetica1>;

<termo> ::= <fator><termo1>;
<termo1> ::= î | "*" <fator> #29 <termo1> | "/" <fator> #30 <termo1>;

<fator> ::= <identificador> #9 <fator1> | const_int #34 | const_float #35 | const_string #36 | "(" <expressao> ")" | "+" <fator> #37 | "-" <fator> #38;
<fator1> ::= î #31| "(" #32 <lista_de_expressoes> ")" #33;
